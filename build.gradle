import groovy.json.JsonBuilder
import groovy.json.JsonSlurper
import org.codehaus.groovy.runtime.EncodingGroovyMethods

project.ext.set("foxweaveVersion", '0.2-SNAPSHOT')
project.ext.set("slf4jVersion", "1.6.4")

apply plugin: 'eclipse'
apply plugin: 'idea'

subprojects {
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'maven'

    project.archivesBaseName = 'foxweave-components' + '-' + project.path.substring(1).replaceAll('/', '-')
    project.buildDir = 'target';
    project.ext.set('groupId', 'com.foxweave');
    project.ext.set('artifactId', project.archivesBaseName);
    project.version = '1.0';

    repositories {
        mavenLocal()
        if (project.foxweaveVersion.endsWith("-SNAPSHOT")) {
            maven {
                url "http://maven.foxweave.com/snapshots/"
            }
        } else {
            maven {
                url "http://maven.foxweave.com/releases/"
            }
        }
        mavenCentral()
    }

    configurations {
        provided {
            description = "provided 'scope'"
            transitive = true
        }
        component {
            description = "component dependency"
            transitive = true
        }
        component.extendsFrom(compile)
        testCompile.extendsFrom(component, provided)
        compile.transitive = true
    }

    dependencies {
        provided group: 'org.slf4j', name: 'slf4j-api', version: project.slf4jVersion
        testCompile group: 'com.foxweave', name: 'foxweave-testutil', version: project.foxweaveVersion
    }

    sourceSets.main.compileClasspath += configurations.provided + configurations.component

    sourceCompatibility = 1.6

	eclipse {
		project {
			name = archivesBaseName
		}
	}

    idea {
        // http://gradle.org/docs/current/dsl/org.gradle.plugins.ide.idea.model.IdeaModule.html
		module {
            name = archivesBaseName
            scopes.PROVIDED.plus += configurations.provided
            scopes.PROVIDED.plus += configurations.component
		}
    }
}

project.ext.set("foxweaveComponents", subprojects.findAll { project ->
    true
})

def imageMimeMappings = [png:"image/png", ico:"image/x-icon", gif:"image/gif", jpeg:"image/jpeg", jpeg:"image/jpeg"]

configure(project.foxweaveComponents) {
    apply plugin: 'application'

    // Application plugin configuration
    applicationDistribution.exclude("**/*")
    applicationName = projectDir.name
    mainClassName = ""

    project.ext.set("startScripts.disabled", true)

	sourceSets {
		main {
			resources {
				include 'foxweave-components.json'
			}
		}
	}

    distZip {
        baseName = applicationName
        archiveName = "${applicationName}.zip"
        into('/') {
            from 'src/main/resources'
            include '**/*'
            exclude '**/foxweave-components.json'
        }
        into('/') {
            from 'src/main/resources'
            include '**/foxweave-components.json'
            filter(
                    org.apache.tools.ant.filters.ReplaceTokens,
                    tokens: [foxweaveVersion: project.foxweaveVersion]
            )
        }

        // Copy all the jar files. Remove the version number from the component jar file.
        into('/lib') {
            from configurations.component
            from "${buildDir}/libs/"
            include '*.jar'
            exclude project.archivesBaseName + '-1.0.jar'
        }
        into('/lib') {
            from configurations.component
            from "${buildDir}/libs/"
            include project.archivesBaseName + '-1.0.jar'
            rename { String fileName ->
                fileName.replace('-1.0.jar', '.jar')
            }
        }
    }

    task embedImages {
        def compInstallPath = new File(System.getProperty("user.home") + "/.foxweave/components/${projectDir.name}");
        def componentJsonFile = new File(compInstallPath, "foxweave-components.json");

        def b64EncodeImage = { imagePath ->
            if (imagePath.startsWith("data:")) {
                return null;
            }

            int fileExtIdx = imagePath.lastIndexOf('.');
            if (fileExtIdx == -1) {
                return null;
            }

            File imageFile = new File(compInstallPath, imagePath);
            if (!imageFile.exists()) {
                throw new IllegalStateException("Component image file '" + imageFile.getAbsolutePath() + "' does not exist.");
            }

            String imageFileExt = imagePath.substring(fileExtIdx + 1);
            String imageMimeType = imageMimeMappings[imageFileExt];

            if (imageMimeType == null) {
                return null;
            }

            Writable writable = EncodingGroovyMethods.encodeBase64(imageFile.getBytes());
            StringWriter writer = new StringWriter();
            writable.writeTo(writer);
            return "data:" + imageMimeType + ";base64," + writer.toString();
        }
        def translateImages = { configElement ->
            if (configElement.smalllogo != null) {
                def b64Image = b64EncodeImage(configElement.smalllogo);
                if (b64Image != null) {
                    configElement.smalllogo_b64 = b64Image;
                }
            }
            if (configElement.largelogo != null) {
                def b64Image = b64EncodeImage(configElement.largelogo);
                if (b64Image != null) {
                    configElement.largelogo_b64 = b64Image;
                }
            }
        }

        doLast {
            if (componentJsonFile.exists()) {
                def jsonSlurper = new JsonSlurper();
                def parsedJson = jsonSlurper.parseText(componentJsonFile.getText("UTF-8"));

                translateImages(parsedJson);
                if (parsedJson.components != null) {
                    for (component in parsedJson.components) {
                        if (component.deprecated == null) {
                            translateImages(component);
                        }
                    }
                }

                def jsonBuilder = new JsonBuilder(parsedJson);
                componentJsonFile.setText(jsonBuilder.toPrettyString(), "UTF-8");
            }
        }
    }

    task copyComponents(type : Copy, dependsOn: [distZip]) {
        description = 'Installs the component to the local filestore (~/.foxweave/components)'
        from zipTree("${buildDir}/distributions/${applicationName}.zip")
        into file(System.getProperty("user.home") + "/.foxweave/components/${projectDir.name}")
    }

    task installComp(dependsOn: [copyComponents, embedImages]) {
    }
}